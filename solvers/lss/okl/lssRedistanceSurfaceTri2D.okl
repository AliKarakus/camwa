/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

// batch process elements
@kernel void lssRedistanceSurfaceTri2D(const dlong Nelements,
                                  const dlong offset,
                                  @restrict const  dfloat *  sgeo,
                                  @restrict const  dfloat *  LIFTT,
                                  @restrict const  dlong  *  vmapM,
                                  @restrict const  dlong  *  vmapP,
                                  @restrict const  int    *  EToB,
                                            const  dfloat time,
                                  @restrict const  dfloat *  x,
                                  @restrict const  dfloat *  y,
                                  @restrict const  dfloat *  z,
                                  @restrict const  dfloat *  sq,                                  
                                  @restrict const  dfloat *  q,
                                  @restrict const  dfloat *  gradq,
                                  @restrict dfloat *  rhsq){

  // for all elements
  for(dlong eo=0;eo<Nelements;eo+=p_NblockS;@outer(0)){

    // @shared storage for flux terms
    @shared dfloat s_pf1[p_NblockS][p_NfacesNfp];
    @shared dfloat s_pf2[p_NblockS][p_NfacesNfp];
    @shared dfloat s_qf1[p_NblockS][p_NfacesNfp];
    @shared dfloat s_qf2[p_NblockS][p_NfacesNfp];

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_NfacesNfp){
            // find face that owns this node
            const int face = n/p_Nfp;

            // load surface geofactors for this face
            const dlong sid   = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const dlong id  = e*p_Nfp*p_Nfaces + n;
            const dlong idM = vmapM[id];
            const dlong idP = vmapP[id];

            // load traces
            const dfloat qM = q[idM];  dfloat qP = q[idP];

            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0){
              lssDirichletConditions2D(bc, time, x[idM], y[idM], nx, ny, qM, &qP);
            }
            
            const dfloat sc = invJ*sJ; 
            // simple upwind
            dfloat unx = 0.0, dnx = 1.0; 
            dfloat uny = 0.0, dny = 1.0; 
            if(nx>= 0){unx = 1.0, dnx = 0.0;}
            if(ny>= 0){uny = 1.0, dny = 0.0;}
            // const dfloat unx =  nx >=0 ? 1.0 : 0.0; 
            // const dfloat dnx =  nx  <0 ? 1.0 : 0.0; 
            // const dfloat uny =  ny >=0 ? 1.0 : 0.0; 
            // const dfloat dny =  ny  <0 ? 1.0 : 0.0; 

            // // sJ/J * (f(q) - q^-)
            // s_pf1[es][n]= sc*nx*( (dnx*qM  + unx*qP) -  qM );
            // s_pf2[es][n]= sc*nx*( (unx*qM  + dnx*qP) -  qM );
            // s_qf1[es][n]= sc*ny*( (dny*qM  + uny*qP) -  qM );
            // s_qf2[es][n]= sc*ny*( (uny*qM  + dny*qP) -  qM );// sJ/J * (f(q) - q^-)
            s_pf1[es][n]= sc*nx*( (dnx*qM  + unx*qP));
            s_pf2[es][n]= sc*nx*( (unx*qM  + dnx*qP));
            s_qf1[es][n]= sc*ny*( (dny*qM  + uny*qP));
            s_qf2[es][n]= sc*ny*( (uny*qM  + dny*qP));
          }
        }
      }
    }

    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // for each node in the element
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        const dlong e = eo + es;
        if(e<Nelements){
          if(n<p_Np){
            const dlong id = e*p_Np+n;
            dfloat p1 = 0.f, p2 = 0.f; 
            dfloat q1 = 0.f, q2 = 0.f; 
            
            // 
            #pragma unroll p_NfacesNfp
              for(int m=0;m<p_NfacesNfp;++m){
                const dfloat L = LIFTT[n+m*p_Np];
                p1 += L*s_pf1[es][m];
                p2 += L*s_pf2[es][m];
                q1 += L*s_qf1[es][m];
                q2 += L*s_qf2[es][m];
              }
            const dfloat sqn  = sq[id];
            const dfloat dqdx = gradq[id + 0*offset];
            const dfloat dqdy = gradq[id + 1*offset];

            p1 +=  dqdx; p2 +=  dqdx; 
            q1 +=  dqdy; q2 +=  dqdy;

            const dfloat P = 0.5f*(p1+p2); 
            const dfloat Q = 0.5f*(q1+q2); 
           
            // LLF Numerical Hamiltonian
            rhsq[id] =  -( sqn*( sqrt( P*P+Q*Q )-1.0) -0.5f*(p1-p2)-0.5*(q1-q2));
          }
        }
      }
    }
  }
}